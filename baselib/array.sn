require("baselib/object.sn")

Array.instance_prototype.eval {
  .reverse: {
    result: @()
    .each [elem, index] { result << .get(-1 - index) }
    result
  }
  
  .select: [yield] {
    result: @()
    .each [elem, index] {
      result << elem if yield(elem, index)
    }
    result
  }
  
  .reject: [yield] {
    result: @()
    .each [elem, index] {
      result << elem unless yield(elem, index)
    }
    result
  }
  
  .detect: [yield] {
    // WHY CAN'T I USE return HEREâ€½
    i: 0
    result: nil
    while i < .length
      if yield(self.get(i), i)
        result: self.get(i)
      end
      i: i + 1
    end
    result
  }
  
  // TODO: ARGUMENT_REFLECTION for initial_value.
  .inject: [initial_value, yield] {
    unless yield
      yield: initial_value
      initial_value: nil
    end
    if initial_value
      index: 0
      memo: initial_value
    else
      index: 1
      memo: self.get(0)
    end
    
    while index < .length
      memo: yield(memo, self.get(index))
      index: index + 1
    end
    
    memo
  }
  
  .getter(#sum) { self.inject(0) [memo, element] { memo + element } }
  
  .getter(#empty?) { .length = 0 }
  
  .getter(#first) { .get(0) }
  .getter(#last)  { .get(-1) }
}
